buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'net.saliman:gradle-cobertura-plugin:2.2.4'
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.3'
    // https://github.com/kt3k/coveralls-gradle-plugin
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
    classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.0'
  }
}

group = 'com.verisign.storm.metrics'
version = '0.1.4'
description = "An IMetricsConsumer that forwards Storm's built-in metrics to a Graphite server for real-time graphing."

apply plugin: 'java'
apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'os-package'

// Helper functions for RPM metadata
def shortCommit = "git rev-parse --short HEAD".execute().in.text.trim()
def buildTime() {
  def df = new java.text.SimpleDateFormat('yyyyMMdd')
  df.setTimeZone(TimeZone.getTimeZone('UTC'))
  return df.format(new Date())
}

ext {
  jdkVersion = '1.7'
  metricsVersion = '3.1.0'

  // RPM metadata
  rpmMaintainer = System.getenv('MAINTAINER') ?: 'change.this@email.com'

  rpmVersion = System.getenv('VERSION') ?: "${project.version}"
  isSnapshot = rpmVersion.endsWith('-SNAPSHOT') ? true : false
  if (isSnapshot) {
    rpmVersion = rpmVersion.replace('-SNAPSHOT', '')
  }

  rpmRelease = '1'
  overriddenRpmRelease = System.getenv('RPM_RELEASE')
  if (overriddenRpmRelease != null) {
    rpmRelease = overriddenRpmRelease
  }
  else if (isSnapshot) {
    defaultBuildNumber = '1'
    releaseNumberIncrement = System.getenv('BUILD_NUMBER') ?: "${defaultBuildNumber}"
    snapshotReleaseTag = "0.${releaseNumberIncrement}.${buildTime()}git${shortCommit}"
    rpmVersion = rpmVersion.replace('-SNAPSHOT', '')
    rpmRelease = snapshotReleaseTag
  }

}

sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

repositories {
  mavenCentral()
  maven { url 'https://clojars.org/repo' }
}

dependencies {

  compile(
    ['org.apache.storm:storm-core:0.9.3'],
    ['com.google.guava:guava:17.0'],
    ["io.dropwizard.metrics:metrics-core:${metricsVersion}"],
    ["io.dropwizard.metrics:metrics-graphite:${metricsVersion}"],
  )

  testCompile(
    ['org.uncommons:reportng:1.1.4'],
    ['org.mockito:mockito-all:1.9.5'],
    ['org.easytesting:fest-assert-core:2.0M10'],
    ['org.testng:testng:6.8.8'],
    // Manually require Google Guice.  This is a required workaround for a known issue with TestNG 6.x.
    // (Gradle will fail to run tests otherwise.)
    ['com.google.inject:guice:3.0']
  )

}

task sourcesJar(type: Jar, dependsOn: 'classes') {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

test {
  useTestNG() {
    suiteXmlBuilder().suite(name: 'storm-graphite', parallel: 'tests', 'thread-count': '1') {
      test (name : 'all-tests') {
        packages {
          'package' (name: 'com.verisign.*')
        }
      }
    }
  }
  options {
    listeners << 'org.uncommons.reportng.HTMLReporter'
    listeners << 'org.uncommons.reportng.JUnitXMLReporter'
  }
  ext.useDefaultListeners = true
  ext.workingDirectory = 'build/'
}

// https://github.com/stevesaliman/gradle-cobertura-plugin
// http://ingorichter.blogspot.ch/2013/03/using-cobertura-with-gradle.html for list of options
cobertura {
  coverageFormats = ['xml', 'html'] // coveralls plugin depends on xml format report
}

task createWrapper(type: Wrapper) {
  gradleVersion = '2.3'
}

ospackage {
  packageName         project.name
  version             rpmVersion
  release             rpmRelease
  summary             description
  packageDescription  description
  packageGroup        'Development/Tools'
  packager            rpmMaintainer
  url                 'https://github.com/verisign/storm-graphite'
  type                BINARY
  arch                NOARCH
  os                  LINUX
  user                'root'
  vendor              'Verisign'
  license             'Apache License, Version 2.0'
}

buildRpm {
  packageName = "${project.name}_slim"
  into '/opt/storm'

  from(jar.outputs.files) {
    into 'lib'
  }
}
buildRpm.dependsOn(jar)

// Creates an RPM that packages an uber jar.
task rpm(type: Rpm) {
  packageName = "${project.name}"
  into '/opt/storm'

  from(shadowJar.outputs.files) {
    into 'lib'
  }
}
rpm.dependsOn(shadowJar)

shadowJar {

  // Relocate Metrics library to prevent conflicts with user code that depends on Metrics, too.
  relocate('com.codahale.metrics', 'com.verisign.storm-graphite.com.codahale.metrics')

  dependencies {
    include(dependency("io.dropwizard.metrics:metrics-core:${metricsVersion}"))
    include(dependency("io.dropwizard.metrics:metrics-graphite:${metricsVersion}"))
  }

}
